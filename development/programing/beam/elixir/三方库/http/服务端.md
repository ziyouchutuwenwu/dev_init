# 服务端

## 代码

### 项目

```sh
mix phx.new web_demo --no-assets --no-gettext --no-dashboard --no-live --no-mailer --no-ecto
```

### router

```elixir
defmodule WebDemoWeb.Router do
  use WebDemoWeb, :router

  pipeline :browser do
    plug :accepts, ["html"]
    plug :fetch_session
    plug :fetch_live_flash
    plug :put_root_layout, html: {WebDemoWeb.Layouts, :root}
    plug :protect_from_forgery
    plug :put_secure_browser_headers
  end

  pipeline :api do
    plug :accepts, ["json"]
  end

  scope "/", WebDemoWeb do
    pipe_through :browser

    get "/", PageController, :home
  end

  scope "/file", WebDemoWeb do
    pipe_through :api

    get "/download", FileController, :download
    put "/put-upload", FileController, :put_upload
    post "/read-upload", FileController, :read_upload
    post "/stream-upload", FileController, :stream_upload
  end

  scope "/api", WebDemoWeb do
    pipe_through :api

    get "/get", DemoController, :get
    get "/uri/:uri_arg", DemoController, :uri_get
    post "/form-post", DemoController, :form_post
    post "/json-post", DemoController, :json_post
  end
end
```

### heex

home.html.heex

```html
<div>
  <form action="/file/read-upload" method="post" enctype="multipart/form-data">
    <input type="file" name="file" />
    <button type="submit">读取整个文件上传</button>
  </form>
</div>

<div>
  <form action="/file/stream-upload" method="post" enctype="multipart/form-data">
    <input type="file" name="file" />
    <button type="submit">流式上传</button>
  </form>
</div>
```

### controller

demo_controller.ex

```elixir
defmodule WebDemoWeb.DemoController do
  require Logger

  use WebDemoWeb, :controller

  def get(conn, params) do
    %{"aaa" => value} = params
    text(conn, "value is #{value}")
  end

  def uri_get(conn, params) do
    %{"uri_arg" => uri_arg_value} = params
    text(conn, "uri #{uri_arg_value}")
  end

  def form_post(conn, params) do
    text(conn, inspect(params))
  end

  def json_post(conn, _params) do
    json = Jason.encode!(conn.body_params)
    text(conn, json)
  end
end
```

file_controller.ex

```elixir
defmodule WebDemoWeb.FileController do
  require Logger
  use WebDemoWeb, :controller

  def put_upload(conn, _params) do
    # 这里拿不到文件名
    {:ok, file_bin, _conn} = Plug.Conn.read_body(conn)
    result = File.write("/tmp/saved_put_bin", file_bin)
    json(conn, %{msg: inspect(result)})
  end

  def read_upload(conn, params) do
    if upload = params["file"] do
      filename = upload.filename
      Logger.debug("filename #{inspect(filename)}")
      dest = System.get_env("HOME") |> Path.join("downloads") |> Path.join(filename)
      Logger.debug("dest #{inspect(dest)}")
      File.cp(upload.path, dest)
    end

    json(conn, %{msg: "上传成功"})
  end

  # def stream_upload(conn, params) do
  #   if upload = params["file"] do
  #     filename = upload.filename
  #     Logger.debug("filename #{inspect(filename)}")
  #     dest = System.get_env("HOME") |> Path.join("downloads") |> Path.join(filename)
  #     Logger.debug("dest #{inspect(dest)}")
  #     File.cp(upload.path, dest)
  #   end

  #   json(conn, %{msg: "上传成功"})
  # end

  def stream_upload(conn, params) do
    upload = params["file"]
    Logger.debug("file #{inspect(upload)}")

    tmp_filepath = upload.path
    filename = upload.filename
    dest = System.get_env("HOME") |> Path.join("downloads") |> Path.join(filename)
    Logger.debug("dest #{inspect(dest)}")

    tmp_filepath
    |> File.stream!()
    # |> Stream.each(&IO.inspect/1)
    |> Stream.into(File.stream!(dest))
    |> Stream.run()

    json(conn, %{msg: "上传成功"})
  end

  def download(conn, _params) do
    file_path = System.get_env("HOME") |> Path.join("downloads") |> Path.join("todo")
    conn |> send_download({:file, file_path})
  end
end
```

### 路径映射

endpoint.ex

```elixir
plug Plug.Static,
  # 用于给外部访问，比如 http://127.0.0.1:4000/uploads/111.png
  at: "/uploads",
  # 文件需要保存的实际路径，读写都用这个
  from: System.get_env("HOME") |> Path.join("downloads")
```

### 大小限制

上传文件大小限制

endpoint.ex

```elixir
plug Plug.Parsers,
  parsers: [:urlencoded, :multipart, :json],
```

改成

```elixir
plug Plug.Parsers,
  # multipart 请求大小限制为 20M
  parsers: [
    :urlencoded,
    {
      :multipart,
      # 20M
      # 单次读取的字节数
      # 每次超时
      length: 20_000_000, read_length: 2_000_000, read_timeout: 30_000
    },
    :json
  ],
```

## 测试

```sh
curl -X POST --data-binary @"/desktop/todo" http://localhost:4000/file/read-upload
curl -X POST -F "todo=@/desktop/todo" http://localhost:4000/file/read-upload
```
