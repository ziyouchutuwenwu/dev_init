# 内网穿透

## 说明

需要公网

```txt
外部用户 ------------------> 公网服务器(8001)
                   |
                   v
公网服务器(8000) ----------> 内网客户端 [通知有新连接]
                   |
                   v
内网客户端 ----------------> 公网服务器(9001端口) [建立隧道]
                   |
                   v
内网客户端 ----------------> 局域网服务(3001端口)
```

## 代码

server.ex

```elixir
defmodule TCPForwarder.Server do
  require Logger
  @agent __MODULE__

  @ets_table :tcp_forwarder_waiters

  def start_link(control_port, port_mappings) do
    Enum.each(port_mappings, fn mapping ->
      unless Keyword.has_key?(mapping, :expose) and Keyword.has_key?(mapping, :through) do
        raise ArgumentError, "Port mapping must contain both :expose and :through keys"
      end
    end)

    :ets.new(@ets_table, [:set, :public, :named_table])

    port_map = %{
      expose_to_proxy:
        Enum.map(port_mappings, fn m -> {Keyword.get(m, :expose), Keyword.get(m, :through)} end)
        |> Enum.into(%{}),
      proxy_to_expose:
        Enum.map(port_mappings, fn m -> {Keyword.get(m, :through), Keyword.get(m, :expose)} end)
        |> Enum.into(%{})
    }

    Agent.start_link(fn -> %{port_map: port_map, clients: %{}} end, name: @agent)

    {:ok, control_socket} =
      :gen_tcp.listen(control_port, [:binary, packet: 4, active: false, reuseaddr: true])

    Logger.info("control server listening on #{control_port}")
    spawn(fn -> accept_control_connections(control_socket) end)

    for mapping <- port_mappings do
      expose_port = Keyword.get(mapping, :expose)
      proxy_port = Keyword.get(mapping, :through)

      {:ok, pub_socket} =
        :gen_tcp.listen(expose_port, [:binary, packet: 0, active: false, reuseaddr: true])

      {:ok, proxy_socket} =
        :gen_tcp.listen(proxy_port, [:binary, packet: 0, active: false, reuseaddr: true])

      Logger.info("public listener on port #{expose_port}, through port #{proxy_port}")
      spawn(fn -> accept_public_connections(pub_socket, expose_port, proxy_port) end)
      spawn(fn -> accept_proxy_connections(proxy_socket, expose_port, proxy_port) end)
    end

    {:ok, %{control_socket: control_socket, port_mappings: port_mappings}}
  end

  defp accept_control_connections(socket) do
    case :gen_tcp.accept(socket) do
      {:ok, client} ->
        Task.start_link(fn ->
          case :gen_tcp.recv(client, 0) do
            {:ok, ports_data} ->
              ports = decode_mappings(ports_data)
              id = :crypto.strong_rand_bytes(8) |> Base.encode16()

              Agent.update(@agent, fn state ->
                put_in(state, [:clients, id], %{socket: client, mappings: ports})
              end)

              Logger.info("client registered with mappings: #{inspect(ports)}")
              control_connection_loop(client, id)

            _ ->
              :gen_tcp.close(client)
          end
        end)

        accept_control_connections(socket)

      {:error, _} ->
        :timer.sleep(1000)
        accept_control_connections(socket)
    end
  end

  defp decode_mappings(<<count::16, rest::binary>>) do
    decode_port_mappings(rest, count, %{})
  end

  defp decode_port_mappings(_binary, 0, acc), do: acc

  defp decode_port_mappings(<<remote_port::16, local_port::16, rest::binary>>, count, acc) do
    decode_port_mappings(rest, count - 1, Map.put(acc, remote_port, local_port))
  end

  defp control_connection_loop(client, id) do
    case :gen_tcp.recv(client, 0) do
      {:ok, _data} ->
        control_connection_loop(client, id)

      {:error, _} ->
        Logger.info("client #{id} disconnected")

        Agent.update(@agent, fn state ->
          %{state | clients: Map.delete(state.clients, id)}
        end)
    end
  end

  defp accept_public_connections(socket, expose_port, proxy_port) do
    case :gen_tcp.accept(socket) do
      {:ok, user} ->
        spawn(fn -> handle_public_connection(user, expose_port, proxy_port) end)
        accept_public_connections(socket, expose_port, proxy_port)

      {:error, _} ->
        :timer.sleep(1000)
        accept_public_connections(socket, expose_port, proxy_port)
    end
  end

  defp handle_public_connection(user, expose_port, proxy_port) do
    case find_client_for_proxy_port(proxy_port) do
      {:ok, %{socket: client_socket, mappings: mappings}} ->
        local_port = Map.get(mappings, proxy_port)
        conn_id = :crypto.strong_rand_bytes(8)
        conn_id_hex = Base.encode16(conn_id)

        message = <<1, expose_port::16, proxy_port::16>> <> conn_id

        Logger.info("new public user connected on port #{expose_port}")

        Logger.info(
          "sending connection request for port #{expose_port} -> proxy #{proxy_port} -> local port #{local_port}, conn_id=#{conn_id_hex}"
        )

        :gen_tcp.send(client_socket, message)
        wait_for_proxy_connection(user, conn_id)

      :error ->
        Logger.error(
          "no client registered for proxy port #{proxy_port} (expose port #{expose_port})"
        )

        :gen_tcp.close(user)
    end
  end

  defp wait_for_proxy_connection(user, conn_id) do
    :ets.insert(@ets_table, {conn_id, self()})
    Process.send_after(self(), {:timeout, conn_id}, 10_000)

    receive do
      {:proxy_connected, proxy} ->
        :ets.delete(@ets_table, conn_id)

        user_port =
          case :inet.port(user) do
            {:ok, port} -> port
            _ -> "unknown"
          end

        Logger.info("new tunnel established from user port #{user_port}")
        forward_connections(user, proxy)

      {:timeout, ^conn_id} ->
        :ets.delete(@ets_table, conn_id)
        conn_id_hex = Base.encode16(conn_id)

        user_port =
          case :inet.port(user) do
            {:ok, port} -> port
            _ -> "unknown"
          end

        Logger.error("proxy connection timed out for #{conn_id_hex} on user port #{user_port}")
        :gen_tcp.close(user)
    end
  end

  def register_proxy_connection(conn_id, proxy_socket) do
    case :ets.lookup(@ets_table, conn_id) do
      [{^conn_id, pid}] ->
        send(pid, {:proxy_connected, proxy_socket})
        Logger.info("proxy connection registered for #{Base.encode16(conn_id)}")

      _ ->
        Logger.warning("no waiting connection found for #{Base.encode16(conn_id)}")
        :gen_tcp.close(proxy_socket)
    end
  end

  defp accept_proxy_connections(socket, expose_port, proxy_port) do
    case :gen_tcp.accept(socket) do
      {:ok, proxy_socket} ->
        case :gen_tcp.recv(proxy_socket, 8, 5000) do
          {:ok, conn_id} when byte_size(conn_id) == 8 ->
            Logger.info("proxy connection received on #{proxy_port} for port #{expose_port}")
            register_proxy_connection(conn_id, proxy_socket)

          {:error, reason} ->
            Logger.error(
              "error receiving conn_id on proxy port #{proxy_port}: #{inspect(reason)}"
            )

            :gen_tcp.close(proxy_socket)
        end

        accept_proxy_connections(socket, expose_port, proxy_port)

      {:error, _} ->
        :timer.sleep(1000)
        accept_proxy_connections(socket, expose_port, proxy_port)
    end
  end

  defp find_client_for_proxy_port(proxy_port) do
    state = Agent.get(@agent, & &1)

    state.clients
    |> Map.values()
    |> Enum.find(fn %{mappings: mappings} -> Map.has_key?(mappings, proxy_port) end)
    |> case do
      nil -> :error
      c -> {:ok, c}
    end
  end

  defp forward_connections(sock1, sock2) do
    pid1 = spawn_link(fn -> relay(sock1, sock2) end)
    pid2 = spawn_link(fn -> relay(sock2, sock1) end)
    ref1 = Process.monitor(pid1)
    ref2 = Process.monitor(pid2)

    receive do
      {:DOWN, ^ref1, :process, _, _} ->
        Process.exit(pid2, :kill)
        clean_close(sock1, sock2)

      {:DOWN, ^ref2, :process, _, _} ->
        Process.exit(pid1, :kill)
        clean_close(sock1, sock2)
    end
  end

  defp relay(from, to) do
    case :gen_tcp.recv(from, 0) do
      {:ok, data} ->
        case :gen_tcp.send(to, data) do
          :ok -> relay(from, to)
          {:error, _} -> clean_close(from, to)
        end

      {:error, _} ->
        clean_close(from, to)
    end
  end

  defp clean_close(sock1, sock2) do
    try do
      :gen_tcp.close(sock1)
    rescue
      _ -> :ok
    end

    try do
      :gen_tcp.close(sock2)
    rescue
      _ -> :ok
    end
  end
end
```

client.ex

```elixir
defmodule TCPForwarder.Client do
  require Logger

  def start_link(server_host, control_port, port_mappings) do
    Enum.each(port_mappings, fn mapping ->
      unless Keyword.has_key?(mapping, :remote) and Keyword.has_key?(mapping, :local) do
        raise ArgumentError, "Port mapping must contain both :remote and :local keys"
      end
    end)

    internal_mappings =
      port_mappings
      |> Enum.map(fn mapping ->
        {Keyword.get(mapping, :remote), Keyword.get(mapping, :local)}
      end)
      |> Enum.into(%{})

    pid =
      spawn_link(fn ->
        connect_loop(String.to_charlist(server_host), control_port, internal_mappings)
      end)

    {:ok, pid}
  end

  defp connect_loop(server_host, control_port, mappings) do
    case :gen_tcp.connect(server_host, control_port, [:binary, packet: 4, active: false]) do
      {:ok, socket} ->
        internal_mappings = convert_mappings(mappings)
        ports_bin = encode_mappings(internal_mappings)

        case :gen_tcp.send(socket, ports_bin) do
          :ok ->
            Logger.info("connected to server #{inspect(server_host)}:#{control_port}")
            Logger.info("registered port mappings: #{inspect(mappings)}")
            control_loop(socket, server_host, control_port, internal_mappings, mappings)

          {:error, reason} ->
            Logger.error("failed to send mappings: #{inspect(reason)}")
            :gen_tcp.close(socket)
            reconnect(server_host, control_port, mappings)
        end

      {:error, reason} ->
        Logger.error("failed to connect to server: #{inspect(reason)}")
        reconnect(server_host, control_port, mappings)
    end
  end

  defp reconnect(server_host, control_port, mappings) do
    Logger.info("reconnecting in 3 seconds...")
    :timer.sleep(3000)
    connect_loop(server_host, control_port, mappings)
  end

  defp convert_mappings(mappings) do
    Enum.map(mappings, fn {remote_port, target} ->
      case parse_addr_port(target) do
        {:ok, {_addr, local_port}} ->
          {remote_port, local_port}

        _ ->
          Logger.error("invalid local service format: #{target}")
          {remote_port, 0}
      end
    end)
    |> Enum.into(%{})
  end

  defp encode_mappings(mappings) do
    count = map_size(mappings)

    pairs =
      Enum.map(mappings, fn {remote_port, local_port} ->
        <<remote_port::16, local_port::16>>
      end)
      |> Enum.join()

    <<count::16>> <> pairs
  end

  defp control_loop(socket, server_host, control_port, internal_mappings, original_mappings) do
    case :gen_tcp.recv(socket, 0) do
      {:ok, <<1, expose_port::16, remote_port::16, conn_id::binary-8>>} ->
        conn_id_hex = Base.encode16(conn_id)
        target = Map.get(original_mappings, remote_port)

        Logger.info(
          "received connection request: expose_port=#{expose_port}, remote_port=#{remote_port}, conn_id=#{conn_id_hex}"
        )

        if target do
          spawn(fn -> handle_new_connection(server_host, remote_port, target, conn_id) end)
        else
          Logger.error("no mapping found for remote port #{remote_port}")
        end

        control_loop(socket, server_host, control_port, internal_mappings, original_mappings)

      {:error, :closed} ->
        Logger.error("control connection closed")
        :gen_tcp.close(socket)
        connect_loop(server_host, control_port, original_mappings)

      {:error, reason} ->
        Logger.error("control connection error: #{inspect(reason)}")
        :gen_tcp.close(socket)
        connect_loop(server_host, control_port, original_mappings)
    end
  end

  defp handle_new_connection(server_host, remote_port, addr_port, conn_id) do
    conn_id_hex = Base.encode16(conn_id)

    case parse_addr_port(addr_port) do
      {:ok, {addr, local_port}} ->
        Logger.info("connecting to proxy #{inspect(server_host)}:#{remote_port}")

        case :gen_tcp.connect(server_host, remote_port, [:binary, packet: 0, active: false]) do
          {:ok, remote} ->
            # 发送 conn_id 用于匹配
            :ok = :gen_tcp.send(remote, conn_id)
            Logger.info("connecting to local service #{addr}:#{local_port}")

            case :gen_tcp.connect(String.to_charlist(addr), local_port, [
                   :binary,
                   packet: 0,
                   active: false
                 ]) do
              {:ok, local} ->
                Logger.info("tunnel established for #{conn_id_hex}")
                forward_connections(remote, local)

              {:error, reason} ->
                Logger.error("cannot connect to local service: #{inspect(reason)}")
                :gen_tcp.close(remote)
            end

          {:error, reason} ->
            Logger.error("cannot connect to proxy: #{inspect(reason)}")
            :ok
        end

      _ ->
        Logger.error("invalid address:port format: #{addr_port}")
        :ok
    end
  end

  defp parse_addr_port(str) do
    case String.split(str, ":") do
      [addr, port_str] ->
        case Integer.parse(port_str) do
          {port, ""} -> {:ok, {addr, port}}
          _ -> :error
        end

      _ ->
        :error
    end
  end

  defp forward_connections(sock1, sock2) do
    pid1 = spawn_link(fn -> relay(sock1, sock2) end)
    pid2 = spawn_link(fn -> relay(sock2, sock1) end)
    Process.flag(:trap_exit, true)

    receive do
      {:EXIT, ^pid1, _} ->
        Process.exit(pid2, :kill)
        clean_close(sock1, sock2)

      {:EXIT, ^pid2, _} ->
        Process.exit(pid1, :kill)
        clean_close(sock1, sock2)
    end
  end

  defp relay(from, to) do
    case :gen_tcp.recv(from, 0) do
      {:ok, data} ->
        case :gen_tcp.send(to, data) do
          :ok -> relay(from, to)
          {:error, _} -> exit(:normal)
        end

      {:error, _} ->
        exit(:normal)
    end
  end

  defp clean_close(sock1, sock2) do
    try do
      :gen_tcp.close(sock1)
    rescue
      _ -> :ok
    end

    try do
      :gen_tcp.close(sock2)
    rescue
      _ -> :ok
    end
  end
end
```

调用

```elixir
TCPForwarder.Server.start_link(8000, [
  [expose: 8001, through: 9001],
  [expose: 8002, through: 9002]
])

TCPForwarder.Client.start_link(
  "127.0.0.1", 8000,
  [
    [remote: 9001, local: "127.0.0.1:3001"],
    [remote: 9002, local: "127.0.0.1:3002"]
  ]
)
```
