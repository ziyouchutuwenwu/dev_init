# 自定义主键关联

默认的字段为主键表用 `id`, 外键表用 `主表名_id`，实际场景未必都是这样

## 参考文档

[参考地址](https://hexdocs.pm/ecto/Ecto.Schema.html#module-schema-attributes)

## 例子

| 表     | 字段   | 字段名       |
| ------ | ------ | ------------ |
| users  | 被引用 | demo_id      |
| avatar | 外键   | user_demo_id |

### 准备工作

参考 [测试项目模板](测试项目模板.md)

### 创建 migration

```sh
mix ecto.gen.migration create_user
```

priv/repo/migrations/xxxx_create_user.exs

```elixir
def change do
  create table(:users) do
    add :name, :string
    add :email, :string
    add :demo_id, :integer

    timestamps()
  end
end
```

如果需要把 demo_id 作为主键

```elixir
def change do
  create table(:users, primary_key: false) do
    add :name, :string
    add :email, :string

    add :demo_id, :integer, primary_key: true

    timestamps()
  end
end
```

```sh
mix ecto.gen.migration create_avatar
```

priv/repo/migrations/xxxx_create_avatar.exs

```elixir
def change do
  create table(:avatars) do
    add :nick_name, :string
    add :pic_url, :string

    timestamps()
  end
end
```

```sh
mix ecto.gen.migration avatar_belongs_to_user
```

priv/repo/migrations/xxxx_avatar_belongs_to_user.exs

```elixir
def change do
  create unique_index(:users, [:demo_id])

  alter table(:avatars) do
    add :user_demo_id, references(
      :users, column: :demo_id, type: :integer,
      on_update: :update_all, on_delete: :delete_all
    )
  end
end
```

### model 定义

lib/orm_demo/user.ex

```elixir
defmodule OrmDemo.User do
  use Ecto.Schema

  # 如果需要把 demo_id 作为主键，这里默认主键设置为 false
  # @primary_key false
  schema "users" do
    field :name, :string
    field :email, :string
    field :demo_id, :integer

    has_one :avatar, OrmDemo.Avatar, foreign_key: :user_demo_id, references: :demo_id

    timestamps()
  end
end
```

lib/orm_demo/avata.ex

```elixir
defmodule OrmDemo.Avatar do
  use Ecto.Schema

  schema "avatars" do
    field :nick_name, :string
    field :pic_url, :string

    belongs_to :user, OrmDemo.User, foreign_key: :user_demo_id

    timestamps()
  end
end
```

lib/demo.ex

```elixir
defmodule Demo do
  def demo do
    alias OrmDemo.{Repo, User, Avatar}
    avatar = %Avatar{nick_name: "aaa", pic_url: "logo.png"}
    user = %User{name: "user1", email: "user1@xxx.com", avatar: avatar, demo_id: 111}
    Repo.insert!(user)
  end
end
```

### 测试

```sh
mix ecto.drop; mix ecto.create; mix ecto.migrate; iex -S mix
```

```elixir
Demo.demo()
```
