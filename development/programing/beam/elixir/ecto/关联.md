# 关联

## 步骤

1. 创建 migration
2. 创建 model
3. 设置关联关系

## 关键字说明

has_one 仅用于 model 语义

belongs_to **对应的字段为表的外键**

| 关联类型 | 主表 model   | 带外键的表 model |
| -------- | ------------ | ---------------- |
| 一对一   | has_one      | belongs_to       |
| 一对多   | has_many     | belongs_to       |
| 多对多   | many_to_many | many_to_many     |

## 准备工作

参考 [测试项目模板](测试项目模板.md)

## 注意

migrations 里面，无论使用外键，还是自定义字段，都可以使用 Orm 的 has_many 等关系

外键的约束具体见 [这里](https://hexdocs.pm/ecto_sql/Ecto.Migration.html#references/2)

## has one

### 例子

一个 User, 一个 Avatar

### 创建 user

#### user 的 migration

```sh
mix ecto.gen.migration create_user
```

priv/repo/migrations/xxxxx_create_user.exs

```elixir
def change do
  create table(:users) do
    add :name, :string
    add :email, :string

    timestamps()
  end
end
```

#### user 的 model

lib/orm_demo/user.ex

```elixir
defmodule OrmDemo.User do
  use Ecto.Schema

  schema "users" do
    field :name, :string
    field :email, :string

    timestamps()
  end
end
```

### 创建 avatar

#### avatar migration

```sh
mix ecto.gen.migration create_avatar
```

priv/repo/migrations/xxxxx_create_avatar.exs

```elixir
def change do
  create table(:avatars) do
    add :nick_name, :string
    add :pic_url, :string

    timestamps()
  end
end
```

#### avatar model

lib/orm_demo/avatar.ex

```elixir
defmodule OrmDemo.Avatar do
  use Ecto.Schema

  schema "avatars" do
    field :nick_name, :string
    field :pic_url, :string

    timestamps()
  end
end
```

### 增加关联关系

创建 migrations，修改表结构

```sh
mix ecto.gen.migration avatar_belongs_to_user
```

priv/repo/migrations/xxxxx_avatar_belongs_to_user.exs

外键关联

```elixir
def change do
  alter table(:avatars) do
    # 在子表里面增加外键, 主表更新删除时候, 设置外键表的对应行为
    add :user_id, references(:users, column: :id, on_update: :update_all, on_delete: :delete_all)
  end
end
```

手动添加字段，不使用外键

```elixir
def change do
  alter table(:avatars) do
    add :user_id, :bigint
  end
  create index(:avatars, [:user_id])
end
```

修改对象类，增加 belongs_to 和 has_one

lib/orm_demo/avatar.ex

```elixir
defmodule OrmDemo.Avatar do
  use Ecto.Schema
  alias OrmDemo.User

  schema "avatars" do
    field :nick_name, :string
    field :pic_url, :string
    belongs_to :user, User

    timestamps()
  end
end
```

lib/orm_demo/user.ex

```elixir
defmodule OrmDemo.User do
  use Ecto.Schema
  alias OrmDemo.Avatar

  schema "users" do
    field :name, :string
    field :email, :string
    has_one :avatar, Avatar

    timestamps()
  end
end
```

lib/demo.ex

```elixir
defmodule Demo do
  alias OrmDemo.{Repo, User, Avatar}

  # 一次性由父表插入, 属性是在 user 表里面设置, 保存只要保存 user 表即可
  def demo do
    avatar = %Avatar{nick_name: "aaa", pic_url: "logo.png"}
    user = %User{name: "user1", email: "user1@xxx.com", avatar: avatar}
    Repo.insert!(user)
  end
end
```

### 持久化测试

```sh
mix ecto.drop; mix ecto.create; mix ecto.migrate --log-migrations-sql; iex -S mix
```

```elixir
Demo.demo()
```

## has many

### 例子说明

一个 User 多个 Post

### user

#### user migration

```sh
mix ecto.gen.migration create_user
```

priv/repo/migrations/xxxxx_create_user.exs

```elixir
def change do
  create table(:users) do
    add :name, :string
    add :email, :string

    timestamps()
  end
end
```

#### user model

lib/orm_demo/user.ex

```elixir
defmodule OrmDemo.User do
  use Ecto.Schema

  schema "users" do
    field :name, :string
    field :email, :string

    timestamps()
  end
end
```

### 创建 post

#### post migration

```sh
mix ecto.gen.migration create_post
```

priv/repo/migrations/xxxxx_create_post.exs

```elixir
def change do
  create table(:posts) do
    add :header, :string
    add :body, :string

    timestamps()
  end
end
```

#### post model

lib/orm_demo/post.ex

```elixir
defmodule OrmDemo.Post do
  use Ecto.Schema

  schema "posts" do
    field :header, :string
    field :body, :string

    timestamps()
  end
end
```

### 增加关联

```sh
mix ecto.gen.migration posts_belongs_to_user
```

priv/repo/migrations/xxxxx_posts_belongs_to_user.exs

```elixir
def change do
  alter table(:posts) do
    # 主表更新删除时候, 设置外键表的对应行为
    add :user_id, references(:users, column: :id, on_delete: :delete_all, on_update: :update_all)
  end
end
```

或者手动添加字段，不使用外键

```elixir
def change do
  alter table(:posts) do
    add :user_id, :bigint
  end
  create index(:posts, [:user_id])
end
```

lib/orm_demo/post.ex

```elixir
defmodule OrmDemo.Post do
  use Ecto.Schema
  alias OrmDemo.User

  schema "posts" do
    field :header, :string
    field :body, :string
    belongs_to :user, User

    timestamps()
  end
end
```

lib/orm_demo/user.ex

```elixir
defmodule OrmDemo.User do
  use Ecto.Schema
  alias OrmDemo.Post

  schema "users" do
    field :name, :string
    field :email, :string
    has_many :posts, Post

    timestamps()
  end
end
```

lib/demo.ex

```elixir
defmodule Demo do
  alias OrmDemo.{Repo, User}

  # 先插入主表数据, 再构建关联数据插入
  def demo do
    user = %User{name: "user1", email: "user1@xxx.com"} |> Repo.insert!

    Ecto.build_assoc(user, :posts, %{header: "post header 1", body: "post body 1"}) |> Repo.insert!

    Ecto.build_assoc(user, :posts, %{header: "post header 2", body: "post body 2"}) |> Repo.insert!
  end
end
```

### 持久化

```sh
mix ecto.drop; mix ecto.create; mix ecto.migrate --log-migrations-sql; iex -S mix
```

```elixir
Demo.demo()
```

## many to many

### 场景例子

Post 和 Tag

### post

#### post 的 migration

```sh
mix ecto.gen.migration create_post
```

priv/repo/migrations/xxxxx_create_post.exs

```elixir
def change do
  create table(:posts) do
    add :header, :string
    add :body, :string

    timestamps()
  end
end
```

#### post 的 model

lib/orm_demo/post.ex

```elixir
defmodule OrmDemo.Post do
  use Ecto.Schema

  schema "posts" do
    field :header, :string
    field :body, :string

    timestamps()
  end
end
```

### 创建 tag

#### tag 的 migration

```sh
mix ecto.gen.migration create_tag
```

priv/repo/migrations/xxxxx_create_tag.exs

```elixir
def change do
  create table(:tags) do
    add :name, :string

    timestamps()
  end
end
```

#### tag 的 model

lib/orm_demo/tag.ex

```elixir
defmodule OrmDemo.Tag do
  use Ecto.Schema

  schema "tags" do
    field :name, :string

    timestamps()
  end
end
```

### 设置关联

```sh
mix ecto.gen.migration create_posts_tags
```

priv/repo/migrations/xxxxx_create_posts_tags

```elixir
def change do
  create table(:posts_tags) do
    # 主表更新删除时候, 设置外键表的对应行为
    add :tag_id, references(:tags, column: :id, on_update: :update_all, on_delete: :delete_all)
    add :post_id, references(:posts, column: :id, on_update: :update_all, on_delete: :delete_all)
  end

  # 这个是多字段联合索引
  create unique_index(:posts_tags, [:tag_id, :post_id])
end
```

或者手动添加字段

```elixir
def change do
  create table(:posts_tags) do
    add :tag_id, :bigint
    add :post_id, :bigint
  end

  # 这个是多字段联合索引
  create unique_index(:posts_tags, [:tag_id, :post_id])
end
```

lib/orm_demo/post.ex

```elixir
defmodule OrmDemo.Post do
  use Ecto.Schema
  alias OrmDemo.Tag

  schema "posts" do
    field :header, :string
    field :body, :string
    timestamps()

    many_to_many :tags, Tag, join_through: "posts_tags"
  end
end
```

lib/orm_demo/tag.ex

```elixir
defmodule OrmDemo.Tag do
  use Ecto.Schema
  alias OrmDemo.Post

  schema "tags" do
    field :name, :string
    timestamps()

    many_to_many :posts, Post, join_through: "posts_tags"
  end
end
```

lib/demo.ex

```elixir
defmodule Demo do
  alias OrmDemo.{Repo, Tag, Post}

  def demo do
    tag1 = %Tag{name: "tag1"} |> Repo.insert!()
    tag2 = %Tag{name: "tag2"} |> Repo.insert!()

    post = %Post{header: "header1", body: "body1"} |> Repo.insert!()

    preloaded = post |> Repo.preload([:tags])

    post_changeset = preloaded |> Ecto.Changeset.change
    post_changeset |> Ecto.Changeset.put_assoc(:tags, [tag1, tag2]) |> Repo.update!
  end
end
```

### 测试持久化

```sh
mix ecto.drop; mix ecto.create; mix ecto.migrate --log-migrations-sql; iex -S mix
```

```elixir
Demo.demo()
```
